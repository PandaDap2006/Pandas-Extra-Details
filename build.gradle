plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.6-SNAPSHOT" apply false
    id "io.github.pacifistmc.forgix" version "1.2.9"
}

architectury {
    minecraft = rootProject.minecraft_version
}

forgix {
    group = "me.pandamods.extra_details"
    mergedJarName = "extra_details-${mod_version}-${minecraft_version}.jar"
    outputDir = "build/libs/merged"

    forge {
        projectName = "forge"
        jarLocation = "build/libs/extra_details-${mod_version}.jar"
    }

    fabric {
        projectName = "fabric"
        jarLocation = "build/libs/extra_details-${mod_version}.jar"
    }
}

subprojects {
    apply plugin: "dev.architectury.loom"

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"

        mappings loom.layered() {
            officialMojangMappings()
            parchment("org.parchmentmc.data:parchment-${rootProject.parchment_version}@zip")
        }
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    base {
        archivesName = rootProject.archives_base_name
    }

    version = rootProject.mod_version
    group = rootProject.maven_group

    repositories {
        maven { url "https://maven.terraformersmc.com/releases/" }

        maven {
            name = 'ParchmentMC'
            url = 'https://maven.parchmentmc.org'
        }
        maven {
            url "https://cursemaven.com"
        }
        maven {
            name = "Modrinth"
            url = "https://api.modrinth.com/maven"
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 17
    }

    java {
        withSourcesJar()
    }
}
